#!ipxe

:start
menu PXE Boot Menu
item --key x exit  Exit to Normal Boot
item --key w winpe Boot WinPE
item --key a auto_install  Auto Install Windows
item --key u ubuntu Boot Ubuntu 24.04.3 (Live)
item --key p proxmox Boot Proxmox VE Installer
item --key r reboot Reboot
choose --timeout 10000 --default exit selected || goto cancel

goto ${selected}

:auto_install
echo ========================================
echo           WARNING - AUTO INSTALL
echo ========================================
echo
echo This will automatically install Windows on this computer
echo
echo WARNING: All data on the hard disk will be ERASED!
echo Computer will be registered to backend and set to maintenance mode
echo Hard disk will be initialized and Windows will be installed
echo
echo Press Ctrl+C to cancel, or wait for countdown...
echo
echo Starting in 3 seconds...
sleep 3
echo Starting automatic installation process...
kernel http://${gateway}:8080/wimboot gui
initrd -n bootx64.efi http://${gateway}:8080/winpe/efi/boot/bootx64.efi bootx64.efi
initrd -n bcd http://${gateway}:8080/winpe/boot/bcd bcd
initrd -n boot.sdi http://${gateway}:8080/winpe/boot/boot.sdi boot.sdi
initrd -n boot.wim http://${gateway}:8080/winpe/sources/boot2.wim boot.wim
imgargs wimboot gui
boot || goto failed

:winpe
kernel http://${gateway}:8080/wimboot gui
initrd -n bootx64.efi http://${gateway}:8080/winpe/efi/boot/bootx64.efi bootx64.efi
initrd -n bcd http://${gateway}:8080/winpe/boot/bcd bcd
initrd -n boot.sdi http://${gateway}:8080/winpe/boot/boot.sdi boot.sdi
initrd -n boot.wim http://${gateway}:8080/winpe/sources/boot.wim boot.wim || goto retry
imgargs wimboot gui
boot || goto failed

:ubuntu
set ubuntu http://${gateway}:8080/ubuntu
sanboot ${ubuntu}/ubuntu-24.04.3-desktop-amd64.iso || goto failed


:proxmox
set prox http://${gateway}:8080/proxmox
kernel ${prox}/boot/linux26
initrd ${prox}/boot/initrd.img
initrd ${prox}/proxmox-ve_9.1-1.iso /proxmox.iso
imgargs linux26 initrd=initrd.img ro ramdisk_size=8388608 rw quiet splash=silent ip=dhcp
boot || goto failed

:retry
echo Boot failed - retrying...
goto winpe

:failed
echo Boot failed
goto start

:sysrescue
kernel http://${gateway}:8080/systemrescue/boot/x86_64/vmlinuz
initrd http://${gateway}:8080/systemrescue/boot/x86_64/sysresccd.img
imgargs vmlinuz initrd=sysresccd.img archisobasedir=systemrescue archiso_http_srv=http://${gateway}:8080/ ip=dhcp copytoram=y
boot || goto failed

:reboot
reboot

:exit
exit

:cancel
echo You cancelled the menu
prompt Press any key to return to the menu...
goto start
